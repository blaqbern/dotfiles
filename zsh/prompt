if [[ ${fpath[(ie)$HOME:a/zshfn]} -gt ${#fpath} ]]; then
  fpath=( $HOME:a/zshfn ${fpath[@]} )
fi

autoload $fpath[1]/*(:t)

precmd () {
  build_prompt
}

build_prompt () {
  local l_prompt_components=( $(blue %~) $(yellow ℤ) '' )
  local r_prompt_components=$(build_r_prompt)

  PROMPT=${l_prompt_components[@]}
  RPROMPT=${r_prompt_components[@]}
}

build_r_prompt () {
  local components

  if [[ -a .git ]]; then
    local current_branch=$(get_current_branch_name)
    local default_branch=$(get_default_branch_name)

    local rebase_merge_status=$(show_merge_or_rebase)
    local status_badges=$(git_status_badges)
    local stash_badge=$(git_stash_badge)
    local vs_origin_default=$(compare_origin_ref $default_branch)
    local vs_origin_current=$(compare_origin_ref $current_branch)

    components=(
      $rebase_merge_status
      $(blue $current_branch)
    )
    [[ -n $status_badges ]] && components+=( $status_badges )
    [[ -n $stash_badge ]] && components+=( $stash_badge )
    [[ -n $vs_origin_default ]] && components=( $(yellow $vs_origin_default) ${components[@]} )
    [[ -n $vs_origin_current ]] && components=( $(magenta $vs_origin_current) ${components[@]} )
  else
    components=( $(yellow ×) )
  fi

  echo $components
}

git_status_badges () {
  local badge=✶ # U+2736
  local status_badges=()

  local git_status=$(git status 2>/dev/null)

  [[ $git_status =~ 'Changes to be committed:' ]] && status_badges+=( $(green $badge) )
  [[ $git_status =~ 'Changes not staged for commit:' ]] && status_badges+=( $(yellow $badge) )
  [[ $git_status =~ 'Untracked files:' ]] && status_badges+=( $(red $badge) )

  [[ ${#status_badges} -gt 0 ]] && echo $status_badges
}

git_stash_badge () {
  [[ -a .git/refs/stash ]] && echo ↪
}

compare_origin_ref () {
  if [[ -n $1 && -a .git/refs/remotes/origin/$1 ]]; then
    local current_ref=$(cat .git/refs/heads/$(get_current_branch_name))
    local origin_ref=$(cat .git/refs/remotes/origin/$1)

    if [[ $current_ref != $origin_ref ]]; then
      local ahead=ᐃ
      local behind=ᐁ

      local merge_base=$(git merge-base $current_ref $origin_ref 2>/dev/null)

      if [[ $origin_ref == $merge_base ]]; then
        echo $ahead
      elif [[ $current_ref == $merge_base ]]; then
        echo $behind
      else
        echo $ahead$behind
      fi
    fi
  else
    echo ∅
  fi
}

show_merge_or_rebase () {
  if [[ -a .git ]]; then
    local merge_rebase_status
    local b

    if [[ -d .git/rebase-apply || -f .git/rebase-merge/interactive ]]; then
      merge_rebase_status="⸨ REBASE ⸩"
    elif [[ -f .git/MERGE_HEAD ]]; then
      merge_rebase_status="⸨ MERGE ⸩"
    fi

    if [[ -n $merge_rebase_status ]]; then
      echo $(yellow $merge_rebase_status)
    fi
  fi
}

function get_default_branch_name () {
	local branch_name

	local control_char=∅
	local branches=( .git/refs/remotes/origin/*(:t) )

	for branch_name in ${branches[@]} $control_char; do
	  [[ $branch_name == 'develop' ]] && break
	  [[ $branch_name == 'main' ]] && break
	  [[ $branch_name == 'master' ]] && break
	  [[ $branch_name == $control_char ]] && branch_name=''
	done

	echo $branch_name
}

function get_current_branch_name () {
	local current_branch=$(cat .git/HEAD)
	echo ${current_branch#ref:\ refs/heads/}
}
